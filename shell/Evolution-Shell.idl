/* -*- Mode: IDL; tab-width: 8; indent-tabs-mode: t; c-basic-offset: 8 -*- */
/*
 * Interface for the Evolution shell.
 *
 * Authors:
 *   Ettore Perazzoli <ettore@ximian.com>
 *
 * Copyright (C) 2000, 2001 Ximian, Inc.
 */

#ifndef _GNOME_EVOLUTION_SHELL_IDL
#define _GNOME_EVOLUTION_SHELL_IDL

#include <Bonobo.idl>

module GNOME {
module Evolution {


	interface ShellView : Bonobo::Unknown {
		/* Should really use a ComponentView i guess */
		void setTitle(in string component, in string title);
		void setComponent(in string component);
	};

	interface Shell : Bonobo::Unknown {
		exception Busy {};
		exception ComponentNotFound {};
		exception InternalError {};
		exception InvalidURI {};
		exception NotFound {};
		exception NotReady {};
		exception UnsupportedSchema {};

		/**
		 * createNewWindow:
		 * @component_id: id or alias of the component to display in the new window.
		 *
		 */
		ShellView createNewWindow (in string component_id)
			raises (NotReady, ComponentNotFound, UnsupportedSchema, InternalError);

		/**
		 * handleURI:
		 * @uri: URI to handle
		 *
		 * This handles the specified URI.  It is different from
		 * `::createNewView' as it doesn't necessarily imply creating a
		 * new ShellView.  (For example, a `mailto:' URI will invoke
		 * the message composer.)
		 */
		void handleURI (in string uri)
			raises (NotReady, ComponentNotFound, NotFound, UnsupportedSchema, InvalidURI, InternalError);

		/**
		 * setLineStatus:
		 *
		 * Set the shell into on-line or off-line mode.
		 */
		void setLineStatus (in boolean online)
			raises (NotReady);

		/*
		 * Lookup a component by id.
		 */
		/*Component findComponent(in string id)
			raises (NotReady, ComponentNotFound);*/
	};
};
};

#endif
